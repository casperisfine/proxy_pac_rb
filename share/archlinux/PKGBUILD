# Maintainer: Max Meyer <dev@fedux.org>
pkgname=local_pac
pkgver=0.0.2
pkgrel=1
pkgdesc="local pacfile serving server"
arch=(i686 x86_64)
url="https://github.com/dg-vrnetze/${pkgname}"
license=('MIT')
depends=(ruby)
makedepends=(rubygems filegen)
source=(http://gems.rubyforge.org/gems/$pkgname-$pkgver.gem)
noextract=($pkgname-$pkgver.gem)
sha256sums=('fee945f21f86f74b52cff1f6045dd294af50e71f9cd530c8e1813c41972c77e3')

package() {
  cd "$srcdir"

  _library_dir=/usr/lib/${pkgname}
  _share_dir="${_library_dir}/gems/${pkgname}-${pkgver}/share"
  _systemd_dir=/usr/lib/systemd/system
  _bin_dir=/usr/bin

  install -d ${pkgdir}/$_library_dir 
  install -d ${pkgdir}/$_bin_dir 
  install -d ${pkgdir}/$_systemd_dir 

  gem install --env-shebang --wrappers --no-ri --no-rdoc --install-dir ${pkgdir}/${_library_dir} $pkgname 

  SOFTWARE_BINARY=$_library_dir/gems/${pkgname}-${pkgver}/bin/${pkgname} SOFTWARE_LIB=/usr/lib/local_pac filegen  ${pkgdir}${_share_dir}/archlinux/startup.erb > ${pkgdir}${_bin_dir}/${pkgname}

  chmod a+x ${pkgdir}/${_bin_dir}/${pkgname}
  rm -r ${pkgdir}/${_library_dir}/cache

  install ${pkgdir}/$_share_dir/systemd/${pkgname}.service ${pkgdir}/$_systemd_dir
  install ${pkgdir}/$_share_dir/systemd/${pkgname}.socket ${pkgdir}/$_systemd_dir
}

# vim:set ts=2 sw=2 et:
